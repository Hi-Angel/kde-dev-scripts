#!/usr/bin/perl
#
# Copyright (C) 2006 Thiago Macieira <thiago@kde.org>
#
# This file is distributed under the Artistic License version 2.1
#

use XML::DOM;
use strict;

my $dirname;
my $svnroot;
my $lastDirCommitRevision;
my $revision;
my $lastCommitAuthor;
my $lastCommitMsg;
my $lastCommitDate;
my @changedPaths;
my @filenames;
my @switchedFilenames;
my @conflictedFilenames;
my $dryRun;
my $quiet;
my $verbose;
my $branch;
my $EDITOR;
my $PAGER;

sub getSvnInfo()
{
  open(INFO, "-|", "svn", "info", "--xml") or die("Could not run svn");
  my $info;
  while (<INFO>)
  {
    $info .= $_;
  }
  close(INFO);
  
  # now parse
  my $parser = new XML::DOM::Parser;
  my $doc = $parser->parse($info);
  
  $dirname = $doc->getElementsByTagName("url")->item(0)->getFirstChild()->toString();
  $svnroot = $doc->getElementsByTagName("root")->item(0)->getFirstChild()->toString();
  $lastDirCommitRevision = $doc->getElementsByTagName("commit")->item(0)->getAttribute("revision");
  
  # trim the root
  $dirname =~ s#^$svnroot##;
  
  $doc->dispose();
}

sub getLastCommitInfo($)
{
  my $target = @_[0];
  
  open(LOG, "-|", "svn", "log", "--xml", "-v", "-rCOMMITTED", $target)
    or die("Could not run svn");
    
  my $log;
  while (<LOG>)
  {
    $log .= $_;
  }
  close(LOG);
  
  # now parse it
  my $parser = new XML::DOM::Parser;
  my $doc = $parser->parse($log);
  
  $revision = $doc->getElementsByTagName("logentry")->item(0)->getAttribute("revision");
  $lastCommitMsg = $doc->getElementsByTagName("msg")->item(0)->getFirstChild()->toString();
  $lastCommitAuthor = $doc->getElementsByTagName("author")->item(0)->getFirstChild()->toString();
  $lastCommitDate = $doc->getElementsByTagName("date")->item(0)->getFirstChild()->toString();
  
  my @changed = $doc->getElementsByTagName("path");
  foreach my $path (@changed)
  {
    push(@changedPaths, $path->getFirstChild()->toString());
  }
  
  $doc->dispose();
}

sub transformToBranch($)
{
  my $path = @_[0];
  if (length($branch) == 0 or $branch eq "trunk")
  {
    # transform to trunk
    $path =~ s,^/(branches|tags)/([^/]+)/[^/]+,/trunk/\2,o;
    return $path;
  }
  elsif ($branch !~ m,/,)
  {
    # transform to branches/XXX/$branch
    if ($path =~ m,^/trunk/,o)
    {
      $path =~ s,^/trunk/([^/]+),/branches/\1/$branch,o;
    }
    else
    {
      $path =~ s,^(/(branches|tags)/[^/]+/)[^/]+,\1$branch,o;
    }
    return $path;
  }
  elsif ($branch =~ m,^work,o)
  {
    $path =~ s,^$dirname,/branches/$branch,o;
    return $path;
  }
  else
  {
    # transform to branches/XXX/$branch
    my ($root, $thebranch) = split(/\//, $branch, 2);
    if ($path =~ m,^/trunk/,o)
    {
      $path =~ s,^/trunk/([^/]+),/$root/\1/$thebranch,o;
    }
    else
    {
      $path =~ s,^/(branches|tags)/([^/]+)/[^/]+,/$root/\2/$thebranch,o;
    }
    return $path;
  }
}

sub checkBranches()
{
  my $target = transformToBranch($dirname);
  print "Porting from $dirname to $target\n";
  
  if ($target eq $dirname)
  {
    print STDERR "Source and target branches are the same.\n";
    exit 1;
  }
}

sub showLog()
{
  my $prettyDate = $lastCommitDate;
  
  $prettyDate =~ s/^(.*)T(.*)\..*Z$/\1 \2 +0000/;
  # mimic svn log:
  print "------------------------------------------------------------------------\n";
  print "r$revision | $lastCommitAuthor | $prettyDate\n";
  print "\n";
  print $lastCommitMsg;
  print "\n\nChanged files:\n";
  
  # file list shown by findAllFiles
}

sub findAllFiles()
{
  foreach my $path (@changedPaths)
  {
    my $entry = $path;
    if (!($entry =~ s#^$dirname/##o))
    {
      print STDERR "Cannot find file \'$svnroot$entry\' in current directory.\n";
      print STDERR "Maybe you need to go up?\n";
      exit 1;
    }
    
    print "  $entry\n";
    push(@filenames, $entry);
    
    # check that it is clean
    open(STATUS, "-|", "svn", "status", $entry);
    my $status;
    while (<STATUS>)
    {
      $status .= $_;
    }
    close(STATUS);
    
    if (length($status))
    {
      print STDERR "File \'$entry\' has local modifications or is not clean. Cannot continue.\n";
      exit 1;
    }
  }

  print "------------------------------------------------------------------------\n";
}

sub run(@)
{
  if ($dryRun or $verbose)
  {
    print join(" ", @_) . "\n";
  }
  if (!$dryRun)
  {
    if ((scalar @_) > 1)
    {
      open(PIPE, "-|", @_);
    }
    else
    {
      my $command = @_[0];
      system($command);
      return "";
    }
    my $output;
    while (<PIPE>)
    {
      $output .= $_;
    }
    print $output unless ($quiet);
    return $output;
  }
  return "";
}

sub rollback()
{
  print "\nRolling back changes\n";
  for my $file (@switchedFilenames)
  {
    run("svn", "revert", $file);
    run("svn", "switch", "-r", $revision, "$svnroot$dirname/$file", $file);
  }
  
  run("rm", "svn-commit.tmp");
}

sub switchAllFiles()
{
  my $target = transformToBranch($dirname);
  print "Switching files to branch $target\n"
    unless ($quiet);

  foreach my $file (@filenames)
  {
    my $output = run("svn", "switch", $svnroot . $target . "/$file", $file);
    push(@switchedFilenames, $file);

    if ($output =~ /^D/)
    {
      print STDERR "I cannot handle file deletions, sorry (\'$file\').\n";
      print STDERR "You will probably have to run \'svn up\' to recover the file.\n";
      rollback();
      exit 1;
    }
  }
}

sub handleConflict($)
{
  my $file = @_[0];
  my $target = transformToBranch($dirname);
  my $leftname = "$file.merge-left.r" . ($revision - 1);
  my $rightname = "$file.merge-right.r$revision";
  my $workingname = "$file.working";

  my $showmenu = 1;
  while (1)
  {
    if ($showmenu)
    {
      print "\nFile \'$file\' has conflicts while merging.\n";
      print "What do you want to do?\n";
      print "\t1. Edit file with $EDITOR\n";
      print "\t2. Show current diff to be committed\n";
      print "\t3. Show the change between " . ($revision - 1) . " and $revision\n";
      print "\t4. View original $dirname/$file (\"left\": before the change)\n";
      print "\t5. View current $dirname/$file (\"right\": after the change)\n";
      print "\t6. View current $target/$file (\"working\")\n";
      print "\t7. Accept original $dirname/$file (\"left\")\n";
      print "\t8. Accept current $dirname/$file (\"right\")\n";
      print "\t9. Accept current $target/$file (\"working\": do not apply merge)\n";
      print "\t0. Revert all and quit\n";
    }
    $showmenu = 0;

    my $answer = <STDIN>;
    $answer =~ s/\r?\n$//;
    if ($answer eq "1")
    {
      run($EDITOR, $file);
      print "Is it resolved? (Y/n)";
      $answer = <STDIN>;
      print "\n";
      if ($answer =~ /y/i or length($answer) == 0)
      {
        run("svn", "resolved", $file);
        return;
      }
    }
    elsif ($answer eq "2")
    {
      run("svn diff $file | $PAGER");
    }
    elsif ($answer eq "3")
    {
      run("diff -u $leftname $rightname | $PAGER");
    }
    elsif ($answer eq "4")
    {
      run($PAGER, "$leftname");
    }
    elsif ($answer eq "5")
    {
      run($PAGER, "$rightname");
    }
    elsif ($answer eq "6")
    {
      run($PAGER, "$workingname");
    }
    elsif ($answer eq "7")
    {
      run("mv", "$leftname", $file);
      run("svn", "resolved", $file);
      return;
    }
    elsif ($answer eq "8")
    {
      run("mv", "$rightname", $file);
      run("svn", "resolved", $file);
      return;
    }
    elsif ($answer eq "9")
    {
      run("mv", "$workingname", $file);
      run("svn", "resolved", $file);
      return;
    }
    elsif ($answer eq "0")
    {
      rollback();
      exit 0;
    }
    else
    {
      $showmenu = 1;
    }
  }
}

sub mergeRevision()
{
  print "Merging revision $revision\n" 
    unless ($quiet);
  my $output = run("svn", "merge", "-r", ($revision - 1) . ":" . $revision, $svnroot . $dirname);
  my @lines = split(/\r?\n/, $output);

  foreach my $line (@lines)
  {
    if ($line =~ /^C +(.+)$/)
    {
      push(@conflictedFilenames, $1)
    }
  }

  foreach my $conflict (@conflictedFilenames)
  {
    handleConflict($conflict);
  }
}

sub showDiff()
{
  print "\nMerging successful\n";
  print "Press enter to see the diff to be committed\n";

  <STDIN>;
  run("svn diff ". join(" ", @filenames) . " | $PAGER");
}

sub createLogMessage()
{
  open(LOG, ">svn-commit.tmp");
  print LOG "INTEGRATION:$dirname $revision\n";
  
  my @lines = split(/\r?\n/, $lastCommitMsg);
  foreach my $line (@lines)
  {
    next if ($line =~ /^CC.?MAIL:.*/);    # don't resend emails
    next if ($line =~ /^GUI:/);
    
    $line =~ s/^(BUG|FEATURE):/CCBUG:/;     # change bug closing to comment
    print LOG $line;
  }
  close(LOG);
}

sub editLogMessage()
{
  print "Press Enter to accept default log or anything else to modify it\n";
  
  my $answer = <STDIN>;
  $answer =~ s/\r?\n$//;
  if (length($answer))
  {
    run($EDITOR, "svn-commit.tmp");
  }
}

sub commit()
{
  print "Committing changes\n";
  run("svn commit -F svn-commit.tmp " . join(" ", @filenames));
  
  print "Restoring old state\n";
  rollback();
}

$EDITOR = $ENV{"EDITOR"};
$PAGER = $ENV{"PAGER"};
$EDITOR = "vi" unless ($EDITOR);
$PAGER = "less" unless ($PAGER);

$dryRun = 0;
$quiet = 0;
$verbose = 1;
$branch = "";
loop: while (@ARGV)
  {
    my $arg = shift @ARGV;
    if ($arg eq "-n")
    {
      $dryRun = 1;
    }
    elsif ($arg eq "-v")
    {
      $verbose = 1;
    }
    elsif ($arg eq "-q")
    {
      $quiet = 1;
    }
    elsif ($arg eq "-b")
    {
      unless (@ARGV)
      {
        print STDERR "Option -b requires an argument\n";
        exit 1;
      }
 
      $branch = shift @ARGV;
    }
    else
    {
      break loop;
    }
  }

getSvnInfo();
getLastCommitInfo($ARGV[0]);
checkBranches();
showLog();
findAllFiles();
switchAllFiles();
mergeRevision();
showDiff();
createLogMessage();
editLogMessage();
commit();